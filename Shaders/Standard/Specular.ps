#include "CameraBuffer.hlsl"
#include "PixelShader.hlsl"

Texture2D DiffuseMap;
Texture2D SpecularMap;
Texture2D NormalMap;
Texture2D AOMap;
SamplerState Sampler;

cbuffer MaterialBuffer
{
	float4 Diffuse;
	float3 Specular;
	float Smoothness;
};

struct PS_In
{
	float4 clipPos : SV_POSITION;
	float4 position : POSITION;
	float4 texCoord : TEXCOORD;
	float4 normal : NORMAL;
	float4 tangent : TANGENT;
};

PS_Out main(PS_In pIn)
{		
	float2 texCoord = pIn.texCoord.xy;

	float4 diffuse = DiffuseMap.Sample(Sampler, texCoord) * Diffuse;
#ifdef ALPHA_TEST
	if(diffuse.a < 0.5)
		discard;
#endif

	float3 albedo = diffuse.rgb;
	float3 f0 = Specular * SpecularMap.Sample(Sampler, texCoord).r;
	float smoothness = Smoothness;
	float ao = AOMap.Sample(Sampler, texCoord).r;
		
	float3x3 tbn;
	tbn[0] = normalize(pIn.tangent.xyz);
	tbn[2] = normalize(pIn.normal.xyz);
	tbn[1] = -cross(tbn[2], tbn[0]);
	
	float3 n = mul(NormalMap.Sample(Sampler, texCoord).xyz * 2.0 - 1.0, tbn);
		
	PS_Out pOut;
	ShadePixel(albedo, ao, f0, smoothness, n, pIn.position.xyz, pOut);		
	return pOut;
}